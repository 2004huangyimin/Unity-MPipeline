#pragma kernel Cull
#pragma kernel Clear
#pragma kernel DecodeR8
#pragma kernel DecodeR16
#include "../CGINC/Plane.cginc"
uint _Count;
struct TerrainChunkBuffer
{
    float2 worldPos;
    float2 scale;
    uint2 vtUV;
};
RWStructuredBuffer<TerrainChunkBuffer> _TerrainChunks;
RWStructuredBuffer<uint> _CullResultBuffer;
RWStructuredBuffer<uint> _DispatchBuffer;
float4 planes[6];
float2 _HeightScaleOffset;
[numthreads(64, 1, 1)]
void Cull(uint id : SV_DISPATCHTHREADID)
{
    if(id >= _Count) return;
    uint len;
    TerrainChunkBuffer buffer = _TerrainChunks[id];
    float3 startPos = float3(buffer.worldPos.x, _HeightScaleOffset.y, buffer.worldPos.y);
    float3 extents = float3(buffer.scale.x, _HeightScaleOffset.x, buffer.scale.x) * 0.5;
    if(BoxIntersect(extents, startPos + extents, planes))
    {
        InterlockedAdd(_DispatchBuffer[1], 1, len);
        _CullResultBuffer[len] = id;
    }
}

[numthreads(1, 1, 1)]
void Clear(uint id : SV_DISPATCHTHREADID)
{
    _DispatchBuffer[1] = 0;
}
RWTexture2D<float> _MainTex;
StructuredBuffer<uint> _TextureBuffer; 
RWTexture2DArray<float> _VirtualHeightmap;
uint _OffsetIndex;
uint _HeightResolution;
[numthreads(8,8,1)]
void DecodeR8(uint2 id : SV_DISPATCHTHREADID)
{
    uint index = id.y * _HeightResolution + id.x;
    uint value = _TextureBuffer[index / 4];
    value >>= (index % 4) * 8;
    value &= 255;
    _MainTex[id] = value / 255.0;
}

[numthreads(8,8,1)]
void DecodeR16(uint2 id : SV_DISPATCHTHREADID)
{
    uint index = id.y * _HeightResolution + id.x;
    uint value = _TextureBuffer[index / 2];
    value >>= (index % 2) * 16;
    value &= 65535;
    _VirtualHeightmap[uint3(id, _OffsetIndex)] = value / 65535.0;
}